apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

version = PROJ_VERSION
group = PROJ_GROUP

//============artifacts===============
if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
        failOnError false
    }
}

//============bintray===============
def pomConfig = {
    scm {
        url PROJ_SITE_URL
    }
    developers {
        developer {
            id DEVELOPER_ID
            name DEVELOPER_NAME
        }
    }
    licenses {
        license {
            name PROJ_LICENCE_NAME
            url PROJ_LICENCE_URL
            distribution PROJ_LICENCE_DEST
        }
    }
}


publishing {
    publications {
        Production(MavenPublication) {
            afterEvaluate {
                from components.release
            }
            groupId PROJ_GROUP
            artifactId PROJ_ARTIFACTID
            version PROJ_VERSION

            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = BINTRAY_REPO
        licenses = BINTRAY_LICENCE
        name = PROJ_ARTIFACTID
        desc = PROJ_DESC
        websiteUrl = PROJ_SITE_URL
        vcsUrl = PROJ_VCS_URL
        websiteUrl = PROJ_SITE_URL
        issueTrackerUrl = PROJ_ISSUE_URL
        publish = true
        publicDownloadNumbers = false
//        dryRun = true
        version {
            name = this.version
//            desc = "${this.version}"
            released = new Date()
//            vcsTag = this.version
        }
    }
}